apply plugin: 'com.android.application'

def getGitDescription = { ->
    try {
        def out = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = out
        }
        return out.toString().trim()
    }
    catch (err) {
        return null
    }
}

android {
    compileSdkVersion 33
    buildToolsVersion '33.0.2'
    defaultConfig {
        applicationId "de.spiritcroc.remotepurchaselist"
        minSdkVersion 16
        targetSdkVersion 33
        versionCode 18
        versionName "18.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
        buildConfigField "String", "GIT_DESCRIBE", "\"" + getGitDescription() + "\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            lintOptions {
                disable 'MissingTranslation'
            }
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.github.spiritcroc:indeterminate-checkbox:1.1@aar'
    implementation 'com.github.bumptech.glide:glide:4.15.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.15.1'
}
